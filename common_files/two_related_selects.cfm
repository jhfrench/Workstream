<!---
<fusedoc fuse="common_files/two_related_selects.cfm">
	<responsibilities>
		I am a common file used for two related selects, included as a CFMODULE.
	</responsibilities>
	<io>
		<in>
			</in>
	</io>

	Edits:
	$Log$

</fusedoc>
--->

<cfsetting enablecfoutputonly="YES">

<!--- TAG PARAMETERS --->
<cfparam name="attributes.query">
<cfparam name="attributes.Display1">
<cfparam name="attributes.Display2">
<cfparam name="attributes.Value1" default="#attributes.Display1#">
<cfparam name="attributes.Value2" default="#attributes.Display2#">
<cfparam name="attributes.Default1" default="(((((none)))))">
<cfparam name="attributes.Default2" default="(((((none)))))">
<cfparam name="attributes.Name1" default="two_related_selects1">
<cfparam name="attributes.Name2" default="two_related_selects2">
<cfparam name="attributes.id1" default="#attributes.Name1#">
<cfparam name="attributes.id2" default="#attributes.Name2#">
<cfparam name="attributes.Size1" default="1">
<cfparam name="attributes.Size2" default="1">
<cfparam name="attributes.class1" default="">
<cfparam name="attributes.class2" default="">
<cfparam name="attributes.Width1" default="">
<cfparam name="attributes.Width2" default="">
<cfparam name="attributes.ForceWidth1" default="">
<cfparam name="attributes.ForceWidth2" default="">
<cfparam name="attributes.ForceWidthChar" default="&nbsp;">
<cfparam name="attributes.EmptyText1" default="">
<cfparam name="attributes.EmptyText2" default="">
<cfparam name="attributes.Message1" default="You must choose an option for #attributes.Name1#.">
<cfparam name="attributes.Message2" default="You must choose an option for #attributes.Name2#.">
<cfparam name="attributes.FormName" default="forms[0]">
<cfparam name="attributes.HTMLBetween" default="">
<cfparam name="attributes.OnChange" default="">
<cfparam name="attributes.ExtraOptions2" default="#IIF((attributes.EmptyText1 EQ "") AND (attributes.Size2 EQ 1), 0, 5)#">
<cfparam name="attributes.AutoSelectFirst" default="Yes">
<cfparam name="attributes.Element1_OnchangeEvent" default="">

<cfset function_name=replacelist(attributes.FormName, "[,]", ",") & attributes.Name1 & "ChangeMenu()">

<!--- "MAGIC" SHORTCUTS FOR THE ONCHANGE HANDLER --->
<cfswitch expression="#attributes.OnChange#">
	<cfcase value="Jump!">
		<cfset attributes.OnChange="document.location=this.options[selectedIndex].value;">
	</cfcase>
	<cfcase value="Submit!">
		<cfset attributes.OnChange="this.attributes.submit();">
	</cfcase>
</cfswitch>



<!--- USE PASSED query WITHIN THIS CODE AS "Myquery" --->
<cfset Myquery=Evaluate("Caller.#attributes.query#")>


<!--- BEGIN JAVASCRIPTING --->
<cfoutput>
	<script language="JavaScript1.1">
	  // javascript code generated by the CF_two_related_selects Cold Fusion tag (Nate Weiss, 4/00)
		// loosely adapted from Nick Heinle's code originally at http://webreference.com/javascript/960902/select_boxes.html
	  var maxlength=10;
	  OneA=new Array;

		var trueLength=OneA.length;
		var lst=OneA.length;

	function require_#attributes.Name1#() {
		  with (document.#attributes.FormName#.#attributes.Name1#) {
			  RetVal=true;
			  if (options[selectedIndex]==null) RetVal=false;
				  else RetVal=!(options[selectedIndex].value=='');
			  if (!RetVal) alert('#attributes.Message1#');
				return RetVal
			}
		}

	function require_#attributes.Name2#() {
		  with (document.#attributes.FormName#.#attributes.Name2#) {
			  RetVal=true;
			  if (options[selectedIndex]==null) RetVal=false;
				  else RetVal=!(options[selectedIndex].value=='');
			  if (!RetVal) alert('#attributes.Message2#');
				return RetVal
			}
		}

		function require_#attributes.Name1#And#attributes.Name2#() {
		  return ((require_#attributes.Name1#()) && (require_#attributes.Name2#()));
		}


		function #function_name# {
		   OneA.length=0;
		   menuNum=document.#attributes.FormName#.#attributes.Name1#.selectedIndex;
		   if (menuNum==null) return;
		<!--- this function gets continued in the next cfoutput section --->
</cfoutput>


<!--- COUNTER VARIABLE WILL HOLD NUMBER OF groupS (OPTIONS IN FIRST SELECT) --->
<cfset Counter=IIF(len(attributes.EmptyText1), 1, 0)>

<!--- CREATE AN "IF" STATEMENT THAT COVERS EACH ITEM IN THE FIRST SELECT BOX --->
<!--- WITHIN THE "IF" STATMENT, PRE-POPULATE ARRAY WITH CORRESPONDING ITEMS FOR SECOND SELECT --->
<cfoutput query="Myquery" group="#attributes.Display1#">
	if (menuNum==#Counter#) {
	  NewOpt=new Array;
		NewVal=new Array;
	<cfset Counter2=IIF(len(attributes.EmptyText2), 1, 0)>
	<cfif len(attributes.EmptyText2)><cfoutput>NewOpt[0]=new Option("#attributes.EmptyText2#", ""); </cfoutput></cfif>
	<cfoutput>NewOpt[#Counter2#]=new Option("#replacelist(Myquery[attributes.Display2][Myquery.currentrow], "\,#Chr(9)#,#Chr(13)##Chr(10)#,',"",#Chr(13)#,#Chr(10)#",  "\\,\t,\n,\',\"",\r,\f")#", "#replacelist(Myquery[attributes.Value2][Myquery.currentrow], "\,#Chr(9)#,#Chr(13)##Chr(10)#,',"",#Chr(13)#,#Chr(10)#",  "\\,\t,\n,\',\"",\r,\f")#"); <cfset Counter2=Counter2+1>
	</cfoutput>} <cfset Counter=Counter+1>
</cfoutput>


<!--- finish up the ChangeMenu() function --->
<cfoutput>
  tot=NewOpt.length;
	lst=document.#attributes.FormName#.#attributes.Name2#.options.length;

	for (i=lst; i > 0; i--) {
	  document.#attributes.FormName#.#attributes.Name2#.options[i]=null;
	}
  for (i=0; i < tot; i++) {
	  document.#attributes.FormName#.#attributes.Name2#.options[i]=NewOpt[i];
	}
  <cfif attributes.AutoSelectFirst is "Yes">
	  document.#attributes.FormName#.#attributes.Name2#.options[0].selected=true;
	</cfif>
}
</script>
</cfoutput>
<!--- DONE WITH JAVASCRIPTING.  NOW WE JUST HAVE TO DISPLAY THE FORM ELEMENTS --->


<!--- ALLOW FOR AUTO-SIZING "SHORTCUT" OF SELECT BOXES --->
<cfif NOT comparenocase(attributes.Size1, "Auto")>
  <!--- MAKE THE FIRST SELECT BE BIG ENOUGH FOR ALL OF ITS OPTIONS --->
  <cfset attributes.Size1=Counter>
</cfif>
<cfif NOT comparenocase(attributes.Size2, "Auto")>
  <!--- MAKE THE SECOND SELECT BE THE SAME SIZE AS THE FIRST --->
  <cfset attributes.Size2=attributes.Size1>
</cfif>

<!--- OUTPUT FIRST SELECT BOX --->
<cfoutput><select name="#attributes.Name1#" id="#attributes.id1#" onchange="#function_name#;#attributes.Element1_OnchangeEvent#" size="#attributes.Size1#" class="#attributes.class1#"<cfif len(attributes.width1)>STYLE="width:#attributes.Width1#"</cfif>></cfoutput>

	<!--- SPECIAL FIRST ITEM, IF REQUESTED --->
	<cfif len(attributes.EmptyText1)><cfoutput><option value="">#attributes.EmptyText1#</option></cfoutput></cfif>
	<!--- GENERATE REMAINING ITEMS FROM query --->
	<cfoutput query="Myquery" group="#attributes.Display1#"><option value="#Myquery[attributes.Value1][Myquery.currentrow]#"<cfif NOT comparenocase(Myquery[attributes.Value1][Myquery.currentrow], attributes.Default1)> selected="selected"</cfif>>#Myquery[attributes.Display1][Myquery.currentrow]#</option></cfoutput>

  <!--- "FORCE WIDTH" OPTION AT BOTTOM, IF REQUESTED --->
	<cfif len(attributes.ForceWidth1)><cfoutput><option value="">#RepeatString(attributes.ForceWidthChar, attributes.ForceWidth1)#</option></cfoutput></cfif>
<cfoutput></SELECT></cfoutput>


<!--- INSERT ANY REQUESTED HTML BETWEEN THE TWO SELECT BOXES --->
<cfoutput>#attributes.HTMLBetween#</cfoutput>

<!--- OUTPUT SECOND SELECT BOX --->
<cfoutput><select name="#attributes.Name2#" id="#attributes.id2#" size="#attributes.Size2#"<cfif len(attributes.onChange)> onChange="#attributes.OnChange#"</cfif> <cfif len(attributes.Width2)>STYLE="width:#attributes.Width2#"</cfif> class="#attributes.class2#"></cfoutput>
	<!--- SPECIAL FIRST ITEM, IF REQUESTED --->
	<cfif len(attributes.EmptyText2)><cfoutput><option value="">#attributes.EmptyText2#</option></cfoutput></cfif>

	<!--- GENERATE REMAINING ITEMS FROM query --->
	<!--- WE ONLY NEED TO OUTPUT THE CHOICES FOR THE FIRST group --->
	<cfif NOT len(attributes.EmptyText1)>
		<cfloop query="Myquery">
			<cfif Myquery[attributes.Value1][Myquery.currentrow] is attributes.Default1>
				<cfoutput><option value="#Myquery[attributes.Value2][Myquery.currentrow]#"<cfif (Myquery[attributes.Value1][Myquery.currentrow] is attributes.Default1) AND (Myquery[attributes.Value2][Myquery.currentrow] is attributes.Default2)> selected="selected"</cfif>>#Myquery[attributes.Display2][Myquery.currentrow]#</option></cfoutput>
			</cfif>
		</cfloop>
	</cfif>

  <cfif val(attributes.ExtraOptions2) GT 0>
	<cfloop from="1" to="#val(attributes.ExtraOptions2)#" index="i">
		<cfoutput><option value=""></option></cfoutput>
	</cfloop>
  </cfif>

  <!--- "FORCE WIDTH" OPTION AT BOTTOM, IF REQUESTED --->
	<cfif len(attributes.ForceWidth2)><cfoutput><option value="">#RepeatString(attributes.ForceWidthChar, attributes.ForceWidth2)#</option></cfoutput></cfif>
<cfoutput></SELECT></cfoutput>


<cfsetting enablecfoutputonly="NO">